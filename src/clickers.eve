Create initial counter
```
commit
  [#counter index: 1, value: 0]
```

Draw UI
```
bind @browser
  [#div #ui children:
    [#div sort: 0, children: [#button #add-counter text: "add counter"]]]
```

Show counters
```
search (@browser, @session)
  counter = [#counter index, value]
  ui = [#ui]
bind @browser
  ui.children += [#div sort: index, children:
    [#button #stepper counter step: -1, text: "-"]
    [#span text: value]
    [#button #stepper counter step: 1, text: "+"]
  ]
```

Handle +/- clicks
```
search (@browser, @session)
  [#click element: [#stepper counter step]]
commit
  counter <- [value: counter.value + step]
```

Handle "add counter" clicks
```
search (@browser, @session)
  [#click element: [#add-counter]]
  counter = [#counter index]
  next-index = max[value: index, given: counter] + 1
commit
  [#counter index: next-index, value: 0]
```

Events hack
```
search
  click = [#click]
commit
  click := none
```