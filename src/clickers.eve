Create initial counter
```
commit
  [#counter index: 1, value: 0]
```

Draw UI
```
bind @browser
  [#div #ui children:
    [#div sort: 0, children: [#button #add-counter text: "add counter"]]]
```

Show counters
```
search @browser @session
  counter = [#counter index, value]
  ui = [#ui]
bind @browser
  ui.children += [#div sort: index, children:
    [#button #stepper counter step: -1, text: "-"]
    [#span text: value]
    [#button #stepper counter step: 1, text: "+"]
  ]
```

Handle +/- clicks
```
search @event @session @browser
  [#click element: [#stepper counter step]]
commit
  counter.value := counter.value + step
```

Handle "add counter" clicks
```
search @event @session @browser
  [#click element: [#add-counter]]
  index = count[given: [#counter]] + 1
commit
  [#counter index, value: 0]
```


## Debugger

Initialize debugger
```
commit
  [#debugger #index value: 0]
```

Capture all clicks
```
search @event @browser @session
  event = [#click element]
  debugger = [#debugger #index value: index]
commit
  [#debugger #event index, event, element, type: "click"]
commit
  debugger.value := index + 1
```

Draw debugger UI
```
bind @browser
  [#div children:
    [#h3 text: "debugger"]
    [#div #debugger #log]
  ]
```

Draw all captured clicks
```
search @session @browser
  [#debugger #event element type index]
  log = [#debugger #log]
bind @browser
  log.children += [#div #event-ui type, element, sort: index, text: "{{type}} on {{element}}"]
```

Show click details
```
search @session @browser
  parent = [#event-ui element type]
  lookup[record: element, attribute, value]
bind @browser
  parent.children += [#span attribute value text: ", {{attribute}}: {{value}}"]
```
